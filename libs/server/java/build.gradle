buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.2.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.3.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$projectDir/gen"
}

clean {
    delete protobuf.generatedFilesBaseDir
}

sourceSets {
    main {
        proto {
            srcDir '../../common/proto'
        }
        java {
            srcDir 'gen/main/java'
            srcDir 'gen/main/grpc'
        }
    }
}


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

shadowJar {
    classifier = null // Avoid classifiers for now...
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Africa\'s Talking SDK',
                'Implementation-Version': properties.getProperty("bintray.version")
    }
}

publishing {
    publications {
        SDK(MavenPublication) {
            artifact shadowJar
            groupId properties.getProperty("bintray.groupId")
            artifactId properties.getProperty("bintray.artifactId")
            version properties.getProperty("bintray.version")
        }
    }
}

bintray {
    dryRun = false
    publish = true
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.key")
    publications = ['SDK']
    pkg {
        repo = properties.getProperty("bintray.repo")
        name = properties.getProperty("bintray.package")
        userOrg = properties.getProperty("bintray.organization")
        vcsUrl = properties.getProperty("bintray.vcsUrl")
        licenses = ['Apache-2.0']
        version {
            name = properties.getProperty("bintray.version")
            desc = 'Africa\'s Talking SDK'
            released  = new Date()
            attributes = ['maturity': 'Development']
        }
    }
}

test {
    dependsOn cleanTest
    testLogging.showStandardStreams = true
}

repositories {
    jcenter()
}

dependencies {

    // grpc
    compile 'io.grpc:grpc-stub:1.4.0'

    compile 'io.grpc:grpc-netty:1.4.0' // Server+Client
    // compile 'io.grpc:grpc-okhttp:1.4.0' Client on Android
    compile 'io.netty:netty-tcnative-boringssl-static:2.0.3.Final'

    compile 'io.grpc:grpc-protobuf:1.4.0' // Server+Client
    // compile 'io.grpc:grpc-protobuf-lite:1.4.0' Client on Android

    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile group: 'junit', name: 'junit', version: '4.11'
}