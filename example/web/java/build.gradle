group 'com.africastalking.example'
version '1.0.0'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'de.fuerstenau.buildconfig'

mainClassName = 'com.africastalking.example.App'

sourceCompatibility = 1.8
targetCompatibility = 1.8

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

buildConfig {
    appName = project.name       // sets value of NAME field
    version = project.version // sets value of VERSION field,
    // 'unspecified' if project.version is not set

    clsName = 'BuildConfig'      // sets the name of the BuildConfig class
    packageName = project.group  // sets the package of the BuildConfig class,
    // 'de.fuerstenau.buildconfig' if project.group is not set
    charset = 'UTF-8'            // sets charset of the generated class,
    // 'UTF-8' if not set otherwise

    buildConfigField 'String', 'USERNAME', properties.getProperty("api.username")
    buildConfigField 'String', 'API_KEY', properties.getProperty("api.key")
    buildConfigField 'String', 'SIP_USERNAME', properties.getProperty("sip.username")
    buildConfigField 'String', 'SIP_PASSWORD', properties.getProperty("sip.password")
    buildConfigField 'String', 'SIP_HOST', properties.getProperty("sip.host")
}

repositories {
    jcenter()
    maven {
        url  "http://dl.bintray.com/africastalking/java"
    }
}

dependencies {
    compile 'org.slf4j:slf4j-simple:1.7.25'
    compile 'com.sparkjava:spark-core:2.6.0'
    compile 'com.sparkjava:spark-template-handlebars:2.3'
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'com.africastalking:server:3.0.6'
    testCompile 'junit:junit:4.12'
}

task runWebApp(type: JavaExec, dependsOn: classes){
    description = "Simple App Server"
    main = "com.africastalking.example.App"
    classpath = sourceSets.main.runtimeClasspath
}